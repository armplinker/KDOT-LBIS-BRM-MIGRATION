CREATE OR REPLACE PROCEDURE BACKUP_KDOTBLP_PORTAL_TABLES AS

  -- THIS PROCEDURE MAKES A BACKUP WITH A UNIQUE NAME  ENDING IN _KT
  -- FOR  ANY TABLES REGISTERED IN TABLE KDOTBLP_PORTAL_TABLES
  -- ALSO INCORPORATED DIRECTLY IN OracleBRM6.sql

  -- ARMarshall, ARM_LLC - 20230102 - created (and in OracleBRM6.sql)
  -- ARMarshall, ARM_LLC - 20230106 - fixed V_SUFFIX which was using a dash to use an underscore before time string
  -- ARMarshall, ARM_LLC - 20230124 - deployed to ESOADEV 
  
  TYPE R_CURSOR IS REF CURSOR;
  CUR R_CURSOR;

  V_Q      VARCHAR2(32767);
  V_SUFFIX VARCHAR2(50) := TRIM(q'[_]' || TO_CHAR(SYSDATE,'yyyymmdd') || q'[_KT]' ); -- no dashes in names...
  V_TN     VARCHAR2(128 BYTE);

BEGIN

  -- commented out for stand-alone
  --IF GET_VAR('SAVEAGENCYSYSTABLES') = 1 THEN
  OPEN CUR FOR
    SELECT TRIM(UPPER(TABLE_NAME)) FROM KDOTBLP_PORTAL_TABLES ORDER BY PROCESSING_ORDER;
  LOOP
    FETCH CUR
      INTO V_TN;
    EXIT WHEN CUR%NOTFOUND;

    DBMS_OUTPUT.PUT_LINE(q'[Archiving KDOT Portal Table ]' || V_TN ||
                         q'[ TO ]' || V_TN || V_SUFFIX || q'[...]');

    V_Q := TRIM(q'[CREATE TABLE ]' || V_TN || V_SUFFIX ||
                q'[ AS SELECT * FROM ]' ||  V_TN );
    BEGIN
      EXECUTE IMMEDIATE V_Q;
      DBMS_OUTPUT.PUT_LINE(q'[Table ]' || V_TN || q'[ archived OK >> ]'|| V_TN );
    EXCEPTION
      WHEN OTHERS THEN
        DBMS_OUTPUT.PUT_LINE(q'[ERROR ARCHIVING TABLE - SQL: ]' || V_Q ||
                             q'[: ]' || SQLERRM);
    END;

  END LOOP;
  CLOSE CUR;
  -- commented out for stand-alone
  -- END IF;
EXCEPTION
  WHEN OTHERS THEN
    BEGIN
      RAISE_APPLICATION_ERROR(-20000, SQLERRM);
    END;
END;
/
